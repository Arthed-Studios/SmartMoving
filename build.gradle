plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.freefair.lombok' version '6.6.1'
}

group = 'com.github.ipecter'
version = '2.0.2'

ConfigurableFileTree libsImplementation = fileTree(dir: 'libs-implementation', include: ['*.jar'])

repositories {
    mavenCentral()
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'worldguard'
        url = "https://maven.enginehub.org/repo/"
    }
    maven {
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        url = 'https://mvnrepository.com/artifact/org.projectlombok/lombok'
    }
}

dependencies {
    implementation libsImplementation
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.5-SNAPSHOT"
    compileOnly "me.clip:placeholderapi:2.11.1"
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly "net.kyori:adventure-platform-bukkit:4.1.0"
    compileOnly "net.kyori:adventure-text-minimessage:4.11.0"
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    implementation project(':NMS')
    implementation project(':VersionIndependentNMS')
    implementation project(':LegacyIndependentNMS')
}

subprojects {
    apply plugin: 'java'
    repositories {
        maven {
            url = 'https://papermc.io/repo/repository/maven-public/'
        }
        maven {
            name = 'codemc-repo'
            url = 'https://repo.codemc.org/repository/nms-local/'
        }
    }
}

def targetJavaVersion = 17

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
